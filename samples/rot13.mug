const rotate = fn(c: u8): u8 {
  const advance = fn(c, basis: u8): u8 {
    (c - basis + 13) % 26 + basis
  }

  match true {
    u8!('a') <= c and c <= u8!('z') { advance(c, u8!('a')) }
    u8!('A') <= c and c <= u8!('Z') { advance(c, u8!('A')) }
    else                            { c                    }
  }
}

pub const encrypt = fn(text: str): str {
  let result = import!("std").arr.FixedArray(chr).with_size(text.len);
  let mut i: usize = 0

  while i < text.len {
    let c = text[i]
    
    result[i] = chr!(rotate(u8!(c)))
    i += 1
  }

  // FixedArray is { usize, u8* }, str is { usize, u8* }
  bitcast!(str, result)
}

pub const decrypt = encrypt

(. test .) const alph = fn {
  test_message("abc")
}

(. test .) const alphanu = fn {
  test_message("abc123")
}

(. test .) const alphanumsy = fn {
  test_message("abc123??#")
}

(. test .) const high_case_alpha = fn {
  test_message("ABC")
}

const test_message = fn(msg: str) {
  let encrypted = encrypt(msg)
  let decrypted = decrypt(encrypted)
  assert!(msg == decrypted)
}